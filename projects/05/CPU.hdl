// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    Not (in=instruction[15], out=AorC); // A or C instruction
    Mux16 (a=ALUout, b=instruction, sel=AorC, out=Ain); // Mux16 -> A-Register

    Or (a=AorC, b=instruction[5], out=loadA);
    ARegister (in=Ain, load=loadA, out=Aout, out[0..14]=addressM);
    Mux16 (a=Aout, b=inM, sel=instruction[12], out=ALU2); // The second unit into ALU

    And (a=instruction[4], b=instruction[15], out=loadD);
    DRegister (in=ALUout, load=loadD, out=ALU1); // The first unit into ALU

    ALU (x=ALU1, y=ALU2, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=outM, out=ALUout, zr=zr, ng=ng);

    And (a=instruction[3], b=instruction[15], out=writeM);

    Not (in=zr, out=Nzr);
    Not (in=ng, out=Nng);
    And (a=Nzr, b=Nng, out=NzrAndng);
    Or (a=zr, b=NzrAndng, out=gez);
    Or (a=zr, b=ng, out=lez);
    Mux (a=false, b=NzrAndng, sel=instruction[0], out=t0);
    Mux (a=zr, b=gez, sel=instruction[0], out=t1);
    Mux (a=ng, b=Nzr, sel=instruction[0], out=t2);
    Mux (a=lez, b=true, sel=instruction[0], out=t3);
    Mux (a=t0, b=t1, sel=instruction[1], out=t4);
    Mux (a=t2, b=t3, sel=instruction[1], out=t5);
    Mux (a=t4, b=t5, sel=instruction[2], out=t6);
    And (a=t6, b=instruction[15], out=PCload);
    PC (in=Aout, load=PCload, inc=true, reset=reset, out[0..14]=pc);


}